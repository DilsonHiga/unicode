app "gen"
    packages {
        pf: "https://github.com/roc-lang/basic-cli/releases/download/0.5.0/Cufzl36_SnJ4QbOoEmiJ5dIpUxBvdB3NEySvuH82Wio.tar.br",
    }
    imports [
        pf.Stdout,
        pf.Stderr,
        pf.Task.{ Task },
        pf.Path.{ Path },
        pf.Arg,
        pf.File,
        "GraphemeBreakTest-15.1.0.txt" as gbpFile : Str,
        Helpers,
        CodePoint,
        InternalCP,
    ]
    provides [main] to pf

main : Task {} I32
main = 
    getFilePath
    |> Task.await writeToFile
    |> Task.onErr \err -> Stderr.line "\(err)"

# TASKS
# TODO move these to a common helper file once module changes and builtin Task are available

getFilePath : Task Path Str
getFilePath =
    args <- Arg.list |> Task.await

    when args |> List.get 1 is
        Ok arg -> Task.ok (Path.fromStr "\(Helpers.removeTrailingSlash arg)/GraphemeTest.roc")
        Err _ -> Task.err "USAGE: roc run GraphemeTest.roc -- path/to/package/"

writeToFile : Path -> Task {} Str
writeToFile = \path ->
    File.writeUtf8 path template
    |> Task.mapErr \_ -> "ERROR: unable to write to \(Path.display path)"
    |> Task.await \_ -> Stdout.line "\nSucessfully wrote to \(Path.display path)\n"

lines : Str
lines =
    gbpFile
    |> Str.split "\n"
    |> List.keepOks \l -> 
        if Str.startsWith l "รท " then 
            Ok l
        else 
            Err NotNeeded
    |> List.map \l ->
        when Str.split l "#" is 
            [exampleStr, ..] -> 
                when splitIntoCPsAndStrs exampleStr is 
                    Ok (cps, strs) -> 
                        
                        cpsStr = 
                            cps 
                            |> List.map Num.toStr 
                            |> Str.joinWith ","
                        
                        strsLenStr = 
                            strs 
                            |> List.len 
                            |> Num.toStr

                        sanitised = 
                            exampleStr
                            |> Str.replaceEach "รท" "%" # replace %
                            |> Str.replaceEach "ร" "x" # replace X
                            |> Str.replaceEach "	" "" # remove tabs
                            

                        """

                        expect # test \(sanitised)
                            cps = [\(cpsStr)] |> List.map InternalCP.fromU32Unchecked
                            result = cps |> CodePoint.toStr |> Result.try Grapheme.split |> Result.map List.len
                            result == Ok \(strsLenStr)
                        """

                    Err ParsingError -> crash "Error parsing line \(l)"
            _ -> crash "Error could not find '#' character in line \(l)"
    |> Str.joinWith "\n"

# we want to split into a list of code points and a list of strings that 
# represent the code points e.g.
splitIntoCPsAndStrs : Str -> Result (List U32, List Str) [ParsingError]
splitIntoCPsAndStrs = \str ->
    splitHelp (Str.toUtf8 str) [] ([],[])

splitHelp : List U8, List U32, (List U32, List Str) -> Result (List U32, List Str) [ParsingError]
splitHelp = \bytes, acc, (cps, strs) -> 
    when bytes is 
        [] -> Ok (cps, strs)
        [first, second, ..] -> 
            # ignore whitespace
            if first == ' ' then 
                splitHelp (List.drop bytes 1) acc (cps, strs)
            # NoBREAK
            else if first == 195 && second == 151 then 
            
                # do nothing, code points are added to accum when seen
                splitHelp (List.drop bytes 2) acc (cps, strs)

            # BREAK
            else if first == 195 && second == 183 then 

                # take accum and make a str
                str =
                    when List.map acc InternalCP.fromU32Unchecked |> CodePoint.toStr is 
                        Ok s1 -> s1
                        Err _ -> crash "UNREACHABLE: got invalid UTF-8 in test data file"
                        
                if str == "" then 
                    # do nothing, no code points in accum
                    splitHelp (List.drop bytes 2) acc (cps, strs) 
                else 
                    splitHelp (List.drop bytes 2) [] (cps, List.append strs str)

            # parse code point add to accum
            else if Helpers.isHex first then 
                { val: hexBytes, rest } = Helpers.takeHexBytes { val: [], rest: bytes }
            
                cp = Helpers.hexBytesToU32 hexBytes

                splitHelp rest (List.append acc cp) ((List.append cps cp), strs)
            else 
                Err ParsingError
        [_] -> Ok (cps, strs) # last byte is always a space ' ' 
        
template = 
    """
    ## WARNING This file is automatically generated. Do not edit it manually. ##
    interface GraphemeTest
        exposes []
        imports [CodePoint, Grapheme, InternalCP]

    \(lines)
    """